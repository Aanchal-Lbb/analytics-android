allprojects {
  buildscript {
    repositories {
      mavenCentral()
    }
  }

  apply plugin: 'idea'

  repositories {
    mavenCentral()
  }

  group = GROUP
  version = VERSION_NAME
}

subprojects {
  project.plugins.whenPluginAdded { plugin ->
    if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name) ||
        "com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
      android {
        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion

        defaultConfig {
          minSdkVersion rootProject.ext.minSdkVersion
          targetSdkVersion rootProject.ext.targetSdkVersion
          versionCode rootProject.ext.versionCode
          versionName rootProject.ext.versionName
        }

        dexOptions {
          preDexLibraries rootProject.ext.preDexLibraries
        }

        lintOptions {
          abortOnError false
        }

        packagingOptions {
          exclude 'LICENSE.txt'
        }

        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_7
          targetCompatibility JavaVersion.VERSION_1_7
        }
      }
    }
  }
}


def isCI = "true".equals(System.getenv("CIRCLECI"))
def preDexEnabled = "true".equals(System.getProperty("preDex", "true"))

ext {
  versionCode = Integer.parseInt(VERSION_CODE)
  versionName = VERSION_NAME

  minSdkVersion = 14
  minSdkVersionWear = 21
  targetSdkVersion = 21
  compileSdkVersion = 21
  buildToolsVersion = '21.1.1'

  preDexLibraries = preDexEnabled && !isCI
}

ext.plugins = [
  android        : 'com.android.tools.build:gradle:1.0.0',
  sdkManager     : 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0',
  androidUnitTest: 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
]

ext.libraries = [
  playWearable   : 'com.google.android.gms:play-services-wearable:6.5.87',
  supportLib     : 'com.android.support:support-v4:21.0.2',
  supportWearable: 'com.google.android.support:wearable:1.0.0',
  googleAnalytics: 'com.google.android.gms:play-services-base:6.5.87'
]

ext.testLibraries = [
  'junit:junit:4.11',
  'org.robolectric:robolectric:2.3',
  'org.mockito:mockito-core:1.10.8',
  'org.assertj:assertj-core:1.7.0',
  'org.powermock:powermock:1.5.6',
  'org.powermock:powermock-module-junit4:1.5.6',
  'org.powermock:powermock-module-junit4-rule:1.5.6',
  'org.powermock:powermock-api-mockito:1.5.6',
  'org.powermock:powermock-classloading-xstream:1.5.6'
]
